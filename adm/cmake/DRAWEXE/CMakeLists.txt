SET(TOOLKIT_APP DRAWEXE)

SET(TOOLKIT_APP_MODULES
	DRAWEXE
)
SET(TOOLKIT_APP_LIBS
	TKDraw
	TKMesh
	TKGeomAlgo
	TKTopAlgo
	TKHLR
	TKGeomBase
	TKG2d
	TKBRep
	TKMath
	TKG3d
	TKernel
	TKService
)
IF(NOT ${PROJECT_NAME}_BUILD_SHARED_LIB AND NOT WIN32)
	SET(TOOLKIT_APP_LIBS ${TOOLKIT_APP_LIBS} ${CMAKE_DL_LIBS} )
ENDIF(NOT ${PROJECT_NAME}_BUILD_SHARED_LIB AND NOT WIN32)

MESSAGE(STATUS "Processing application: ${TOOLKIT_APP} (${TOOLKIT_APP_MODULES})")
SET(TOOLKIT_APP_SOURCE_FILES)
FOREACH(MODULE ${TOOLKIT_APP_MODULES})
	# add all .cxx/*.c files or each module
	FILE(GLOB source_files
		${${PROJECT_NAME}_SOURCE_DIR}/src/${MODULE}/*.cxx
		${${PROJECT_NAME}_SOURCE_DIR}/src/${MODULE}/*.c
		${${PROJECT_NAME}_SOURCE_DIR}/drv/${MODULE}/*.cxx
		${${PROJECT_NAME}_SOURCE_DIR}/drv/${MODULE}/*.c)
	#MESSAGE(STATUS "${source_files}")
	FOREACH(module_source_file ${source_files})
		IF (WIN32)
			# For compilers under Windows a define must be set per file to correctly set the export macro
			SET_SOURCE_FILES_PROPERTIES(${module_source_file} PROPERTIES COMPILE_FLAGS "-D__${MODULE}_DLL")
		ENDIF(WIN32)
	ENDFOREACH(module_source_file ${source_files})

	# append these source files to the list of source files of the toolkit
	SET(TOOLKIT_APP_SOURCE_FILES ${TOOLKIT_APP_SOURCE_FILES} ${source_files}) 
	# required include paths
	INCLUDE_DIRECTORIES(${${PROJECT_NAME}_SOURCE_DIR}/src/${MODULE} ${${PROJECT_NAME}_SOURCE_DIR}/drv/${MODULE})
ENDFOREACH(MODULE ${TOOLKIT_APP_MODULES})
ADD_EXECUTABLE(${TOOLKIT_APP} ${TOOLKIT_APP_SOURCE_FILES})
SET_TARGET_PROPERTIES(${TOOLKIT_APP} PROPERTIES
	INSTALL_RPATH "${${PROJECT_NAME}_INSTALL_PREFIX}/${${PROJECT_NAME}_INSTALL_LIB_DIR};${${PROJECT_NAME}_INSTALL_PREFIX}/${${PROJECT_NAME}_INSTALL_PACKAGE_LIB_DIR}"
)
# Set dependencies for thit ToolKit
LIST(REMOVE_DUPLICATES TOOLKIT_APP_LIBS)
TARGET_LINK_LIBRARIES(${TOOLKIT_APP} ${TOOLKIT_APP_LIBS} ${WIN32_LIBS})

IF(WIN32 AND (CMAKE_CONFIGURATION_TYPES OR NMAKE))
	#  Those are private libraries
	INSTALL(TARGETS ${TOOLKIT_APP}
		RUNTIME DESTINATION ${${PROJECT_NAME}_INSTALL_BIN_DIR}         CONFIGURATIONS Release RelWithDebInfo MinSizeRel COMPONENT RuntimeLibraries
		LIBRARY DESTINATION ${${PROJECT_NAME}_INSTALL_PACKAGE_LIB_DIR} CONFIGURATIONS Release RelWithDebInfo MinSizeRel COMPONENT RuntimeLibraries
		ARCHIVE DESTINATION ${${PROJECT_NAME}_INSTALL_PACKAGE_LIB_DIR} CONFIGURATIONS Release RelWithDebInfo MinSizeRel COMPONENT Development
	)

	INSTALL(TARGETS ${TOOLKIT_APP}
		RUNTIME DESTINATION ${${PROJECT_NAME}_INSTALL_BIN_DEBUG_DIR}         CONFIGURATIONS "Debug" COMPONENT RuntimeLibraries
		LIBRARY DESTINATION ${${PROJECT_NAME}_INSTALL_PACKAGE_LIB_DEBUG_DIR} CONFIGURATIONS "Debug" COMPONENT RuntimeLibraries
		ARCHIVE DESTINATION ${${PROJECT_NAME}_INSTALL_PACKAGE_LIB_DEBUG_DIR} CONFIGURATIONS "Debug" COMPONENT Development
	)
ELSE(WIN32 AND (CMAKE_CONFIGURATION_TYPES OR NMAKE))

#  Those are private libraries
INSTALL(TARGETS ${TOOLKIT_APP}
	RUNTIME DESTINATION ${${PROJECT_NAME}_INSTALL_BIN_DIR}         COMPONENT RuntimeLibraries
	LIBRARY DESTINATION ${${PROJECT_NAME}_INSTALL_PACKAGE_LIB_DIR} COMPONENT RuntimeLibraries
	ARCHIVE DESTINATION ${${PROJECT_NAME}_INSTALL_PACKAGE_LIB_DIR} COMPONENT Development
)

ENDIF(WIN32 AND (CMAKE_CONFIGURATION_TYPES OR NMAKE))
