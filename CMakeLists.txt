# REQUIREMENTS: this script needs CMake 2.6 or higher. Downloads are available at http://www.cmake.org

# How to build OCE using CMake under Unix (Linux, Darwin etc.)?
# 1. Copy this file, as-is, to the top-level OCE folder
# 2. Make a build directory somewhere outside the OCE source tree
# 3. cmake "relative/path/to/OCE"
# 4. make
# 5. make install (or sudo make install)

# How to build OCE using CMake under Windows (Linux, Darwin etc.)?
# 1. Copy this file, as-is, to the top-level folder
# 2. Launch the cmake-gui, and select the top-level folder
# 3. You can choose whatever you want as an install directory
# 4. Click 'Configure'
# 5. Choose your generator
# 6. When configure is achived, you can change the variable with a red background
# 7. Click 'Generate'
# 8. From your IDE, open the generated makefile and run the compilation.

PROJECT(OCE)

SET(OCE_VERSION_MAJOR 0)
SET(OCE_VERSION_MINOR 3)
SET(OCE_VERSION_PATCH 0)

SET(OCE_VERSION ${OCE_VERSION_MAJOR}.${OCE_VERSION_MINOR}.${OCE_VERSION_PATCH})

SET(OCE_ABI_SOVERSION 0)
SET(OCE_ABI_VERSION 0.0.0)

# Set the minimum version of cmake required to 2.6
CMAKE_MINIMUM_REQUIRED( VERSION 2.6 )

# Define helper macro OPTION_WITH_DEFAULT
MACRO( OPTION_WITH_DEFAULT OPTION_NAME OPTION_STRING OPTION_DEFAULT )
        IF( NOT DEFINED ${OPTION_NAME} )
                SET( ${OPTION_NAME} ${OPTION_DEFAULT} )
        ENDIF( NOT DEFINED ${OPTION_NAME} )
        OPTION( ${OPTION_NAME} "${OPTION_STRING}" ${${OPTION_NAME}} )
ENDMACRO( OPTION_WITH_DEFAULT OPTION_NAME OPTION_STRING OPTION_DEFAULT )

IF( "${CMAKE_MAJOR_VERSION}.${CMAKE_MINOR_VERSION}" LESS 2.5 )
	MESSAGE( FATAL_ERROR "CMake >= 2.6.0 required" )
ENDIF( "${CMAKE_MAJOR_VERSION}.${CMAKE_MINOR_VERSION}" LESS 2.5 )

############################
# Check 32/64 bit platform #
############################
IF (${CMAKE_SIZEOF_VOID_P} MATCHES "8") # It is 64bit, otherwise 32 bit systems match 4
	ADD_DEFINITIONS(-D_OCC64)
	SET(BIT 64)
ELSE (${CMAKE_SIZEOF_VOID_P} MATCHES "8")
  SET(BIT 32)
ENDIF(${CMAKE_SIZEOF_VOID_P} MATCHES "8")

MESSAGE(STATUS "Build ${BIT}bit")

IF (NOT MSVC)
	IF( NOT DEFINED ${PROJECT_NAME}_BUILD_TYPE )
		SET( ${PROJECT_NAME}_BUILD_TYPE "Release" CACHE STRING "Build type" ) # By default set release build
	ENDIF( NOT DEFINED ${PROJECT_NAME}_BUILD_TYPE )
	SET( CMAKE_BUILD_TYPE ${${PROJECT_NAME}_BUILD_TYPE} CACHE INTERNAL "Build type,
		immutable" FORCE )
ENDIF(NOT MSVC)

IF(CMAKE_BUILD_TOOL STREQUAL "nmake")
        SET(NMAKE TRUE)
ENDIF(CMAKE_BUILD_TOOL STREQUAL "nmake")

IF(MSVC)
	OPTION_WITH_DEFAULT( ${PROJECT_NAME}_INSTALL_PDB_FILES "Install PDB files (debug informations)" ON )
ENDIF(MSVC)

IF(MSVC)
	OPTION_WITH_DEFAULT( ${PROJECT_NAME}_USE_BUNDLE "Use OCE Windows Bundle" OFF )
ENDIF(MSVC)

OPTION_WITH_DEFAULT( ${PROJECT_NAME}_BUILD_SHARED_LIB "Build shared ${PROJECT_NAME} libs" ON )
OPTION_WITH_DEFAULT( ${PROJECT_NAME}_MODEL "Build model components" ON )
OPTION_WITH_DEFAULT( ${PROJECT_NAME}_OCAF "Build application framework" ON )
OPTION_WITH_DEFAULT( ${PROJECT_NAME}_DATAEXCHANGE "Build data exchange" ON )
OPTION_WITH_DEFAULT( ${PROJECT_NAME}_DRAW "Build test harness \(DRAW\)" OFF )
OPTION_WITH_DEFAULT( ${PROJECT_NAME}_VISUALISATION "Build visualisation component needed by other modules" ON )
OPTION_WITH_DEFAULT( ${PROJECT_NAME}_VISUALISATION_FULL "Build full visualisation component" ON )
OPTION_WITH_DEFAULT( ${PROJECT_NAME}_WITH_GL2PS "Build with GL2PS" OFF )
OPTION_WITH_DEFAULT( ${PROJECT_NAME}_WITH_FREEIMAGE "Build with FreeImage" OFF )
OPTION_WITH_DEFAULT( ${PROJECT_NAME}_EXTRA_WARNINGS "Enable Maximum Warnings Level" OFF )
MARK_AS_ADVANCED(${PROJECT_NAME}_EXTRA_WARNINGS)

IF( NOT DEFINED ${PROJECT_NAME}_MULTITHREAD_LIBRARY)
	SET( ${PROJECT_NAME}_MULTITHREAD_LIBRARY "NONE" CACHE STRING "(values are: NONE TBB OPENMP)" )
ENDIF( NOT DEFINED ${PROJECT_NAME}_MULTITHREAD_LIBRARY)

IF( "${CMAKE_MAJOR_VERSION}.${CMAKE_MINOR_VERSION}" GREATER 2.8 )
	SET_PROPERTY(CACHE ${PROJECT_NAME}_MULTITHREAD_LIBRARY PROPERTY STRINGS NONE TBB OPENMP)
ENDIF( "${CMAKE_MAJOR_VERSION}.${CMAKE_MINOR_VERSION}" GREATER 2.8 )

OPTION( ${PROJECT_NAME}_MULTITHREAD_LIBRARY "Multithread library" ${${PROJECT_NAME}_MULTITHREAD_LIBRARY} )

IF( NOT DEFINED ${PROJECT_NAME}_INSTALL_PREFIX )
	IF( WIN32 )
		SET(${PROJECT_NAME}_INSTALL_PREFIX
			"c:/${PROJECT_NAME}-${${PROJECT_NAME}_VERSION}")
	ELSE( WIN32 )
		SET(${PROJECT_NAME}_INSTALL_PREFIX
			"/usr/local")
	ENDIF( WIN32 )
ENDIF( NOT DEFINED ${PROJECT_NAME}_INSTALL_PREFIX )

SET( ${PROJECT_NAME}_INSTALL_PREFIX ${${PROJECT_NAME}_INSTALL_PREFIX} CACHE
	PATH "Install prefix prepended to target to create install location" )
SET( CMAKE_INSTALL_PREFIX "${${PROJECT_NAME}_INSTALL_PREFIX}" CACHE INTERNAL "Prefix
	prepended to install directories if target destination is not absolute,
	immutable" FORCE )

IF( ${PROJECT_NAME}_BUILD_SHARED_LIB )
	SET( ${PROJECT_NAME}_LIBRARY_TYPE SHARED )
ELSE( ${PROJECT_NAME}_BUILD_SHARED_LIB )
	SET( ${PROJECT_NAME}_LIBRARY_TYPE STATIC )
	IF ( WIN32 )
		ADD_DEFINITIONS("-DHAVE_NO_DLL")
	ENDIF( WIN32 )
ENDIF( ${PROJECT_NAME}_BUILD_SHARED_LIB )

# MSVC specific options
IF(MSVC_IDE)
   IF (NOT DEFINED ${PROJECT_NAME}_USE_MSVC_EXPRESS )
       SET (${PROJECT_NAME}_USE_MSVC_EXPRESS OFF)
   ENDIF (NOT DEFINED ${PROJECT_NAME}_USE_MSVC_EXPRESS)
   
   OPTION( ${PROJECT_NAME}_USE_MSVC_EXPRESS "Use a Visual C++ express IDE"
     ${${PROJECT_NAME}_USE_MSVC_EXPRESS} )
ENDIF(MSVC_IDE)  

#
# Check components dependencies
#
SET(${PROJECT_NAME}_FOUNDATION TRUE)
IF(${PROJECT_NAME}_DRAW AND NOT ${PROJECT_NAME}_DATAEXCHANGE)
	SET(${PROJECT_NAME}_DATAEXCHANGE ON CACHE BOOL "Build data exchange" FORCE)
ENDIF(${PROJECT_NAME}_DRAW AND NOT ${PROJECT_NAME}_DATAEXCHANGE)
IF(${PROJECT_NAME}_DATAEXCHANGE AND NOT ${PROJECT_NAME}_OCAF)
	SET(${PROJECT_NAME}_OCAF ON CACHE BOOL "Build application framework" FORCE)
ENDIF(${PROJECT_NAME}_DATAEXCHANGE AND NOT ${PROJECT_NAME}_OCAF)
IF(${PROJECT_NAME}_OCAF AND NOT ${PROJECT_NAME}_VISUALISATION)
	SET(${PROJECT_NAME}_VISUALISATION ON CACHE BOOL "Build visualisation component needed by other modules" FORCE)
ENDIF(${PROJECT_NAME}_OCAF AND NOT ${PROJECT_NAME}_VISUALISATION)
IF(${PROJECT_NAME}_VISUALISATION AND NOT ${PROJECT_NAME}_MODEL)
	SET(${PROJECT_NAME}_MODEL ON CACHE BOOL "Build model components" FORCE)
ENDIF(${PROJECT_NAME}_VISUALISATION AND NOT ${PROJECT_NAME}_MODEL)
IF(${PROJECT_NAME}_VISUALISATION_FULL AND NOT ${PROJECT_NAME}_VISUALISATION)
	SET(${PROJECT_NAME}_VISUALISATION ON CACHE BOOL "Build model components needed by other modules" FORCE)
ENDIF(${PROJECT_NAME}_VISUALISATION_FULL AND NOT ${PROJECT_NAME}_VISUALISATION)
IF(${PROJECT_NAME}_DRAW AND NOT ${PROJECT_NAME}_VISUALISATION_FULL)
	SET(${PROJECT_NAME}_VISUALISATION_FULL ON CACHE BOOL "Build full visualisation component" FORCE)
ENDIF(${PROJECT_NAME}_DRAW AND NOT ${PROJECT_NAME}_VISUALISATION_FULL)

SET(INSTALL_RESOURCE_FILES)
FILE(GLOB OS_TCL_SCRIPTS RELATIVE ${${PROJECT_NAME}_SOURCE_DIR} src/OS/*.tcl)
SET(INSTALL_RESOURCE_FILES ${INSTALL_RESOURCE_FILES}
	${OS_TCL_SCRIPTS}
	src/UnitsAPI/Lexi_Expr.dat
	src/UnitsAPI/Units.dat )

FIND_PACKAGE(Threads)
IF(${PROJECT_NAME}_MULTITHREAD_LIBRARY STREQUAL "TBB")
	FIND_PATH(TBB_INCLUDE_DIR tbb/tbb.h DOC "Location of header files for TBB (containing tbb/tbb.h)" ${CMAKE_SYSTEM_INCLUDE_PATH})
	IF(TBB_INCLUDE_DIR)
		FIND_LIBRARY( TBB_LIBRARY tbb "Path to the TBB library" )
	ENDIF(TBB_INCLUDE_DIR)
	MARK_AS_ADVANCED(TBB_INCLUDE_DIR TBB_LIBRARY)
ENDIF(${PROJECT_NAME}_MULTITHREAD_LIBRARY STREQUAL "TBB")
IF(${PROJECT_NAME}_MULTITHREAD_LIBRARY STREQUAL "OPENMP")
	FIND_PACKAGE(OpenMP)
ENDIF(${PROJECT_NAME}_MULTITHREAD_LIBRARY STREQUAL "OPENMP")

IF (${PROJECT_NAME}_USE_BUNDLE)
	FIND_PATH(OCE_BUNDLE_ROOT_PATH oce-bundle-root.empty "Path for the OCE-win-bundle")
ENDIF(${PROJECT_NAME}_USE_BUNDLE)

IF(${PROJECT_NAME}_VISUALISATION AND ${PROJECT_NAME}_WITH_FREEIMAGE)
	FIND_PATH(FREEIMAGE_INCLUDE_DIR FreeImagePlus.h DOC "Location of header files for FreeImage" ${CMAKE_SYSTEM_INCLUDE_PATH})
	IF(FREEIMAGE_INCLUDE_DIR)
		FIND_LIBRARY( FREEIMAGE_LIBRARY freeimageplus "Path to the freeimage library" )

		IF(CMAKE_CONFIGURATION_TYPES OR NMAKE)
			FIND_LIBRARY( FREEIMAGE_LIBRARY_DEBUG freeimageplusd "Path to the freeimage debug library" )
		ENDIF(CMAKE_CONFIGURATION_TYPES OR NMAKE)
	ENDIF(FREEIMAGE_INCLUDE_DIR)
	MARK_AS_ADVANCED(FREEIMAGE_INCLUDE_DIR FREEIMAGE_LIBRARY)
ENDIF(${PROJECT_NAME}_VISUALISATION AND ${PROJECT_NAME}_WITH_FREEIMAGE)

#
# X11 is needed on Unix to compile OSD/OSD_FontMgr.cxx
#
IF (NOT WIN32)
	FIND_PACKAGE(X11 REQUIRED)
ENDIF (NOT WIN32)

IF(${PROJECT_NAME}_VISUALISATION_FULL)
	IF (NOT WIN32)
		FIND_LIBRARY( X11_Xmu_LIB Xmu PATHS /usr/openwin/lib )
	ENDIF(NOT WIN32)

	IF (NOT APPLE)
	  FIND_PACKAGE(OpenGL REQUIRED)
	ELSE (NOT APPLE)
	  # under OSX, builtin FindOpenGl.cmake returns Cocoa OpenGL implementation
	  # oce requires the X11 based OpenGL
	  SET(OPENGL_INCLUDE_DIR /usr/X11R6/include/)
	  SET(OPENGL_LIBRARIES /usr/X11R6/lib/libGL.dylib /usr/X11R6/lib/libGLU.dylib)
	ENDIF (NOT APPLE)

	## FreeType / FTGL
	IF(NOT ${PROJECT_NAME}_USE_BUNDLE)
		FIND_PACKAGE(Freetype REQUIRED)
	ELSE (NOT ${PROJECT_NAME}_USE_BUNDLE)
		FIND_LIBRARY(FREETYPE_LIBRARY freetype)
	ENDIF(NOT ${PROJECT_NAME}_USE_BUNDLE)

	FIND_PATH(FTGL_V212_INCLUDE_DIR FTGL/FTFace.h DOC "Location of header files for FTGL 2.1.2" ${CMAKE_SYSTEM_INCLUDE_PATH})
	FIND_PATH(FTGL_INCLUDE_DIR FTGL/ftgl.h "Location of header files for FTGL > 2.1.2" ${CMAKE_SYSTEM_INCLUDE_PATH})
	IF(NOT FTGL_V212_INCLUDE_DIR AND FTGL_INCLUDE_DIR)
		ADD_DEFINITIONS(-DHAVE_FTGL_NEWER212)
	ENDIF(NOT FTGL_V212_INCLUDE_DIR AND FTGL_INCLUDE_DIR)
	FIND_LIBRARY(FTGL_LIBRARY ftgl "Location of ftgl library (any version)")
	IF(CMAKE_CONFIGURATION_TYPES OR NMAKE)
		FIND_LIBRARY(FTGL_LIBRARY_DEBUG ftgld "Location of ftgl debug library (any version)")
		FIND_LIBRARY(FREETYPE_LIBRARY_DEBUG freetyped "Location of freetype debug library")
	ENDIF(CMAKE_CONFIGURATION_TYPES OR NMAKE)
	MARK_AS_ADVANCED(X11_Xmu_LIB FTGL_V212_INCLUDE_DIR FTGL_INCLUDE_DIR FTGL_LIBRARY)

	## GL2PS
	IF(${PROJECT_NAME}_WITH_GL2PS)
		FIND_PATH(GL2PS_INCLUDE_DIR gl2ps.h DOC "Location of header files for GL2PS (containing gl2ps.h)" ${CMAKE_SYSTEM_INCLUDE_PATH})
		IF(GL2PS_INCLUDE_DIR)
			FIND_LIBRARY( GL2PS_LIBRARY gl2ps "Path to the gl2ps library" )
			IF(CMAKE_CONFIGURATION_TYPES OR NMAKE)
				FIND_LIBRARY( GL2PS_LIBRARY_DEBUG gl2psd "Path to the gl2ps debug library" )
			ENDIF(CMAKE_CONFIGURATION_TYPES OR NMAKE)
		ENDIF(GL2PS_INCLUDE_DIR)
		MARK_AS_ADVANCED(GL2PS_INCLUDE_DIR GL2PS_LIBRARY)
	ENDIF(${PROJECT_NAME}_WITH_GL2PS)

	MESSAGE(STATUS "X11 LIBS: ${X11_LIBRARIES}")
	MESSAGE(STATUS "OPENGL LIBS: ${OPENGL_LIBRARIES}")
	MESSAGE(STATUS "FREETYPE LIBS: ${FREETYPE_LIBRARIES}")
	MESSAGE(STATUS "FREETYPE INCLUDES: ${FREETYPE_INCLUDE_DIRS}")
	MESSAGE(STATUS "THREAD LIB: ${CMAKE_THREAD_LIBS_INIT}")

	FILE(GLOB TEXTURES_RGB_FILES RELATIVE ${${PROJECT_NAME}_SOURCE_DIR} src/Textures/*.rgb)
	FILE(GLOB FONTMFT_MFT_FILES RELATIVE ${${PROJECT_NAME}_SOURCE_DIR} src/FontMFT/*.mft)
	SET(INSTALL_RESOURCE_FILES ${INSTALL_RESOURCE_FILES} ${FONTMFT_MFT_FILES} ${TEXTURES_RGB_FILES})
ENDIF(${PROJECT_NAME}_VISUALISATION_FULL)

IF(${PROJECT_NAME}_OCAF)
	FILE(GLOB XMLOCAFRESOURCE_XSD_FILES RELATIVE ${${PROJECT_NAME}_SOURCE_DIR} src/XmlOcafResource/*.xsd)
	SET(INSTALL_RESOURCE_FILES ${INSTALL_RESOURCE_FILES}
		${XMLOCAFRESOURCE_XSD_FILES}
		src/StdResource/MDTV-Standard.xwd
		src/StdResource/Plugin
		src/StdResource/Standard
		src/StdResource/Standard.us
		src/StdResource/StandardLite
		src/StdResource/TObj
		src/StdResource/XCAF
		src/StdResource/dftree.tcl )
ENDIF(${PROJECT_NAME}_OCAF)

IF(${PROJECT_NAME}_DATAEXCHANGE)
	SET(INSTALL_RESOURCE_FILES ${INSTALL_RESOURCE_FILES}
		src/SHMessage/SHAPE.fr
		src/SHMessage/SHAPE.us
		src/XSMessage/IGES.fr
		src/XSMessage/IGES.us
		src/XSMessage/XSTEP.fr
		src/XSMessage/XSTEP.us
		src/XSTEPResource/IGES
		src/XSTEPResource/STEP )
ENDIF(${PROJECT_NAME}_DATAEXCHANGE)

IF(${PROJECT_NAME}_DRAW)
	IF (NOT APPLE)
	  FIND_PACKAGE(TCL REQUIRED)
	ELSE(NOT APPLE)
	  # under OSX, should use a X11 based TCL/TK, for instance fink's implementation
	  SET(FINK_INSTALL_PATH /sw)
	  MARK_AS_ADVANCED(FINK_INSTALL_PATH)
	  SET(TCL_LIBRARY ${FINK_INSTALL_PATH}/lib/libtcl.dylib)
	  SET(TCL_INCLUDE_PATH ${FINK_INSTALL_PATH}/include)
	  SET(TCL_TCLSH ${FINK_INSTALL_PATH}/bin/tclsh)
	  SET(TK_LIBRARY ${FINK_INSTALL_PATH}/lib/libtk.dylib)
	  SET(TK_INCLUDE_PATH ${FINK_INSTALL_PATH}/include)
	  SET(TK_WISH ${FINK_INSTALL_PATH}/bin/wish)
	  MARK_AS_ADVANCED(TCL_INCLUDE_PATH TK_INCLUDE_PATH TCL_LIBRARY TK_LIBRARY)
	ENDIF(NOT APPLE)
	MESSAGE(STATUS "TCL LIB: ${TCL_LIBRARY}")
	MESSAGE(STATUS "TK LIB: ${TK_LIBRARY}")
	FILE(GLOB DRAWRESOURCES_TCL_FILES RELATIVE ${${PROJECT_NAME}_SOURCE_DIR} src/DrawResources/*.tcl)
	SET(INSTALL_RESOURCE_FILES ${INSTALL_RESOURCE_FILES}
		${DRAWRESOURCES_TCL_FILES}
		src/DrawResources/DRAW.doc
		src/DrawResources/DRAW.info
		src/DrawResources/DrawPlugin
		src/DrawResources/demo
		src/DrawResources/demo.bat
		src/DrawResources/idoc
		src/DrawResources/lamp.ico
		src/DrawResources/mdltest
		src/DrawResources/mkdoc
		src/DrawResources/tdoc
		src/DrawResources/test2xl
		src/DrawResources/vmdltest
		src/DrawResources/wing.brep )
ENDIF(${PROJECT_NAME}_DRAW)
 
IF (${PROJECT_NAME}_USE_BUNDLE AND OCE_BUNDLE_ROOT_PATH)
	MESSAGE("Using OCE Bundle")
	IF (${PROJECT_NAME}_VISUALISATION_FULL)
		SET(FTGL_INCLUDE_DIR ${OCE_BUNDLE_ROOT_PATH}/include/FTGL CACHE PATH "" FORCE)
		SET(FREETYPE_INCLUDE_DIR_freetype2 ${OCE_BUNDLE_ROOT_PATH}/include/freetype CACHE PATH "" FORCE)
		SET(FREETYPE_INCLUDE_DIR_ft2build ${OCE_BUNDLE_ROOT_PATH}/include/freetype CACHE PATH "" FORCE)
		SET(FREETYPE_INCLUDE_DIRS ${FREETYPE_INCLUDE_DIR_freetype2} ${FREETYPE_INCLUDE_DIR_ft2build}) 
		SET(FREETYPE_LIBRARY ${OCE_BUNDLE_ROOT_PATH}/Win${BIT}/lib/freetype.lib CACHE FILEPATH "" FORCE)
		SET(FREETYPE_LIBRARIES ${OCE_BUNDLE_ROOT_PATH}/Win${BIT}/lib/freetype.lib CACHE FILEPATH "" FORCE)
		SET(FREETYPE_LIBRARY_DEBUG ${OCE_BUNDLE_ROOT_PATH}/Win${BIT}/libd/freetyped.lib CACHE FILEPATH "" FORCE)
		SET(FTGL_LIBRARY ${OCE_BUNDLE_ROOT_PATH}/Win${BIT}/lib/ftgl.lib CACHE FILEPATH "" FORCE)
		SET(FTGL_LIBRARY_DEBUG ${OCE_BUNDLE_ROOT_PATH}/Win${BIT}/libd/ftgld.lib CACHE FILEPATH "" FORCE)
		ADD_DEFINITIONS(-DHAVE_FTGL_NEWER212)
	ENDIF (${PROJECT_NAME}_VISUALISATION_FULL)

	IF(${PROJECT_NAME}_VISUALISATION AND ${PROJECT_NAME}_WITH_GL2PS)
		SET(GL2PS_LIBRARY ${OCE_BUNDLE_ROOT_PATH}/Win${BIT}/lib/gl2ps.lib CACHE FILEPATH "" FORCE)
		SET(GL2PS_LIBRARY_DEBUG ${OCE_BUNDLE_ROOT_PATH}/Win${BIT}/libd/gl2psd.lib CACHE FILEPATH "" FORCE)
		SET(GL2PS_INCLUDE_DIR ${OCE_BUNDLE_ROOT_PATH}/include/gl2ps CACHE PATH "" FORCE)
	ENDIF(${PROJECT_NAME}_VISUALISATION AND ${PROJECT_NAME}_WITH_GL2PS)

	IF(${PROJECT_NAME}_VISUALISATION AND ${PROJECT_NAME}_WITH_FREEIMAGE)
		SET(FREEIMAGE_LIBRARY ${OCE_BUNDLE_ROOT_PATH}/Win${BIT}/lib/FreeImage.lib CACHE FILEPATH "" FORCE)
		SET(FREEIMAGE_LIBRARY_DEBUG ${OCE_BUNDLE_ROOT_PATH}/Win${BIT}/libd/FreeImaged.lib CACHE FILEPATH "" FORCE)
		SET(FREEIMAGE_INCLUDE_DIR ${OCE_BUNDLE_ROOT_PATH}/include/FreeImage CACHE PATH "" FORCE)
	ENDIF(${PROJECT_NAME}_VISUALISATION AND ${PROJECT_NAME}_WITH_FREEIMAGE)
ENDIF(${PROJECT_NAME}_USE_BUNDLE AND OCE_BUNDLE_ROOT_PATH)

IF(MSVC)
	SET( CMAKE_DEBUG_POSTFIX "d" )
ENDIF(MSVC)

INCLUDE_DIRECTORIES(${CMAKE_CURRENT_BINARY_DIR}/build_inc
	${CMAKE_CURRENT_SOURCE_DIR}/inc)

###################################
# Architecture-specific libraries #
###################################
IF(WIN32)
	SET(LM)
	SET(CSF_gdi32_LIB gdi32)
	SET(CSF_advapi32_LIB advapi32)
	SET(CSF_user32_LIB user32)
	SET(CSF_kernel32_LIB kernel32)
	SET(CSF_SOCKETLibs_LIB ws2_32)
	SET(CSF_wsock32_LIB ws2_32)
ELSE(WIN32)
	SET(LM "-lm")
	SET(WIN32_LIBS "")
	SET(CSF_gdi32_LIB "")
	SET(CSF_advapi32_LIB "")
	SET(CSF_user32_LIB "")
	SET(CSF_kernel32_LIB "")
	SET(CSF_SOCKETLibs_LIB "")
	SET(CSF_wsock32_LIB "")
ENDIF(WIN32)

###################################################
# Check required headers, functions and libraries #
###################################################
IF (NOT WIN32)
    # Check headers
    # I think these should all be check cxx headers, as I think there is no c
    # compilation anywhere in ${PROJECT_NAME}
    INCLUDE(CheckIncludeFile)
    #INCLUDE(CheckIncludeFiles)
    CHECK_INCLUDE_FILE(string.h HAVE_STRING_H)
    CHECK_INCLUDE_FILE(dlfcn.h HAVE_DLFCN_H)
    CHECK_INCLUDE_FILE(dl.h HAVE_DL_H)
    CHECK_INCLUDE_FILE(ieeefp.h HAVE_IEEEFP_H)
    CHECK_INCLUDE_FILE(time.h HAVE_TIME_H)
    CHECK_INCLUDE_FILE(sys/time.h HAVE_SYS_TIME_H)
    CHECK_INCLUDE_FILE(pwd.h HAVE_PWD_H)
    CHECK_INCLUDE_FILE(sys/statvfs.h HAVE_SYS_STATVFS_H)
    CHECK_INCLUDE_FILE(sys/vfs.h HAVE_SYS_VFS_H)
    CHECK_INCLUDE_FILE(sys/param.h HAVE_SYS_PARAM_H)
    CHECK_INCLUDE_FILE(osfcn.h HAVE_OSFCN_H)
    CHECK_INCLUDE_FILE(netdb.h HAVE_NETDB_H)
    CHECK_INCLUDE_FILE(sys/ioctl.h HAVE_SYS_IOCTL_H)
    CHECK_INCLUDE_FILE(net/if.h HAVE_NET_IF_H)
    CHECK_INCLUDE_FILE(sys/systeminfo.h HAVE_SYS_SYSTEMINFO_H)
    CHECK_INCLUDE_FILE(sys/utsname.h HAVE_SYS_UTSNAME_H)
    CHECK_INCLUDE_FILE(sysent.h HAVE_SYSENT_H)
    CHECK_INCLUDE_FILE(unistd.h HAVE_UNISTD_H)
    CHECK_INCLUDE_FILE(sys/unistd.h HAVE_SYS_UNISTD_H)
    CHECK_INCLUDE_FILE(sys/socket.h HAVE_SYS_SOCKET_H)
    CHECK_INCLUDE_FILE(ndir.h HAVE_NDIR_H)
    CHECK_INCLUDE_FILE(sys/ndir.h HAVE_SYS_NDIR_H)
    CHECK_INCLUDE_FILE(sys/dir.h HAVE_SYS_DIR_H)
    CHECK_INCLUDE_FILE(signal.h HAVE_SIGNAL_H)
    CHECK_INCLUDE_FILE(sys/signal.h HAVE_SYS_SIGNAL_H)
    CHECK_INCLUDE_FILE(sigfpe.h HAVE_SIGFPE_H)
    CHECK_INCLUDE_FILE(floatingpoint.h HAVE_FLOATINGPOINT_H)
    CHECK_INCLUDE_FILE(sys/machsig.h HAVE_SYS_MACHSIG_H)
    CHECK_INCLUDE_FILE(sys/siginfo.h HAVE_SYS_SIGINFO_H)
    CHECK_INCLUDE_FILE(malloc.h HAVE_MALLOC_H)
    CHECK_INCLUDE_FILE(strings.h HAVE_STRINGS_H)
    CHECK_INCLUDE_FILE(sys/stat.h HAVE_SYS_STAT_H)
    CHECK_INCLUDE_FILE(sys/sem.h HAVE_SYS_SEM_H)
    CHECK_INCLUDE_FILE(sys/ipc.h HAVE_SYS_IPC_H)
    CHECK_INCLUDE_FILE(sys/times.h HAVE_SYS_TIMES_H)
    CHECK_INCLUDE_FILE(dirent.h HAVE_DIRENT_H)
    INCLUDE(CheckIncludeFileCXX)
    CHECK_INCLUDE_FILE_CXX(iomanip.h OCE_HAVE_IOMANIP_H)
    CHECK_INCLUDE_FILE_CXX(limits OCE_HAVE_LIMITS)
    CHECK_INCLUDE_FILE_CXX(climits OCE_HAVE_CLIMITS)
    CHECK_INCLUDE_FILE_CXX(limits.h OCE_HAVE_LIMITS_H)
    CHECK_INCLUDE_FILE_CXX(values.h HAVE_VALUES_H)
    CHECK_INCLUDE_FILE_CXX(fstream OCE_HAVE_FSTREAM)
    CHECK_INCLUDE_FILE_CXX(ios HAVE_IOS)
    CHECK_INCLUDE_FILE_CXX(iomanip OCE_HAVE_IOMANIP)
    CHECK_INCLUDE_FILE_CXX(iostream OCE_HAVE_IOSTREAM)
    CHECK_INCLUDE_FILE_CXX(fstream.h OCE_HAVE_FSTREAM_H)
    CHECK_INCLUDE_FILE_CXX(ios.h HAVE_IOS_H)
    CHECK_INCLUDE_FILE_CXX(iostream.h OCE_HAVE_IOSTREAM_H)
    CHECK_INCLUDE_FILE_CXX(stdlib.h HAVE_STDLIB_H)
    CHECK_INCLUDE_FILE_CXX(sys/types.h HAVE_SYS_TYPES_H)
    CHECK_INCLUDE_FILE_CXX(sys/select.h HAVE_SYS_SELECT_H)
    CHECK_INCLUDE_FILE_CXX(X11/extensions/readdisplay.h HAVE_X11_EXTENSIONS_READDISPLAY_H)
    CHECK_INCLUDE_FILE_CXX(X11/extensions/multibuf.h HAVE_X11_EXTENSIONS_MULTIBUF_H)
    CHECK_INCLUDE_FILE_CXX(sys/filio.h HAVE_SYS_FILIO_H)
    CHECK_INCLUDE_FILE_CXX(sys/mman.h HAVE_SYS_MMAN_H)
    CHECK_INCLUDE_FILE_CXX(libc.h HAVE_LIBC_H)
    # Check library functions
    INCLUDE(CheckFunctionExists)
    CHECK_FUNCTION_EXISTS(alloca HAVE_ALLOCA)
    CHECK_FUNCTION_EXISTS(memcmp HAVE_MEMCMP)
    CHECK_FUNCTION_EXISTS(signal HAVE_SIGNAL)
    CHECK_FUNCTION_EXISTS(statfs HAVE_STATFS)
    CHECK_FUNCTION_EXISTS(statvfs HAVE_STATVFS)
    CHECK_FUNCTION_EXISTS(finite HAVE_FINITE)
    SET(CMAKE_REQUIRED_LIBRARIES sunmath)
    CHECK_FUNCTION_EXISTS(ieee_handler HAVE_SUNMATH)
    SET(CMAKE_REQUIRED_LIBRARIES)
    INCLUDE(CheckTypeSize)
    SET(CMAKE_EXTRA_INCLUDE_FILES sys/time.h)
    CHECK_TYPE_SIZE(tm TM_IN_SYS_TIME)
    SET(CMAKE_EXTRA_INCLUDE_FILES)
    INCLUDE (CheckCSourceCompiles)
    CHECK_C_SOURCE_COMPILES("
    #include <sys/time.h>
    #include <sys/sem.h>
    int main(){union semun c;return 0;}
    "
    SEMUN_DEFINED)
    IF (SEMUN_DEFINED)
    	CHECK_C_SOURCE_COMPILES("
    	#include <sys/time.h>
    	#include <sys/sem.h>
    	int main(){union semun c;c.__buf = 0;return 0;}
    	"
    	SEMUN_BUF_DEFINED)
    ENDIF (SEMUN_DEFINED)
    CHECK_C_SOURCE_COMPILES("
    #include <sys/sem.h>
    static struct sembuf event_flag;
    int main(){int status; int *semid;status = semop(*semid,event_flag,1);return 0;}
    "
    SEMOP_NO_REFERENCE)

    CHECK_C_SOURCE_COMPILES("
    #include <sys/sem.h>
    int get_semaphore(int *semid,int *event,int *value)
    {
        int status;
        status = semctl((*semid),*event,GETVAL,value);
        return 0;
    }
    int main(){ int *a,*b,*c;
    if(get_semaphore(a,b,c))
        return 0;
    return 0;}
    "
    SEMCTL_NO_REFERENCE)

    INCLUDE (CheckCXXSourceCompiles)
    CHECK_CXX_SOURCE_COMPILES("
    #include <ios>
    int main(){static const int input = (std::ios::in)|(std::ios::nocreate);
    static const int output = (std::ios::out);return 0;}
    "
    DEF_IOS_OK)
    CHECK_CXX_SOURCE_COMPILES("
    #include <iostream>
    int main(){std::cout << std::cout.form(\"%9.3e\", 3.14159627e-4) << std::endl;return 0;}
    "
    OSTREAM_FORM_OK)
    INCLUDE(TestBigEndian)
    TEST_BIG_ENDIAN(WORDS_BIGENDIAN)
    SET(STDC_HEADERS TRUE) # Only for really old systems, being lazy
    SET(STACK_DIRECTION 0) # Don't have a test for this, TODO?


# TODO: check all headers/functions/libraries that are checked in configure.ac original OCC script
ENDIF(NOT WIN32)

#######################################################
# Check platforms - Define specific compilation flags #
#######################################################
IF(UNIX)
  IF(APPLE)
    ######### MacOSX ###########
    MESSAGE(STATUS "MacOSX platform detected")
    SET(PLATFORM Darwin)
  ELSE(APPLE)
    ######### Unix/Linux ###########
    MESSAGE(STATUS "Unix/Linux platform detected")
    SET(PLATFORM Unix)
  ENDIF(APPLE)
  ADD_DEFINITIONS(-DHAVE_CONFIG_H -DCSFDB -DOCC_CONVERT_SIGNALS)
ELSE(UNIX)
  IF(WIN32)
    ######### Windows ###########
    MESSAGE(STATUS "Windows system detected")
    ADD_DEFINITIONS(-DWNT -DWIN32 -D_WINDOWS -DCSFDB)
    SET(PLATFORM win)
  ELSE(WIN32)
    MESSAGE(STATUS "Unknown platform")
  ENDIF(WIN32)
ENDIF(UNIX)
# TODO: better detection of different Unices (Linux, Solaris etc.)
# TODO: add CXX compiler flags for each platform

# Enable Extra warnings for each compiler
IF( ${PROJECT_NAME}_EXTRA_WARNINGS)
  IF(MSVC)
    ADD_DEFINITIONS("/W4 /wd4100 /wd4206 /wd4127")
  ENDIF(MSVC)
  IF(CMAKE_COMPILER_IS_GNUCXX)
    ADD_DEFINITIONS("-Wall")
  ENDIF(CMAKE_COMPILER_IS_GNUCXX)
  IF(BORLAND)
    #TODO
  ENDIF(BORLAND)
ENDIF(${PROJECT_NAME}_EXTRA_WARNINGS)

################################################
# Define output path for generated libraries:  #
# platform/compiler-build_type-bits           #
# for instance:                                #
#     ./win32/bin/vc7.1-release-64             #
#     ./win32/bin/vc9-debug-32                 #
#     ./Unix/i386-debug-64                      #
################################################
IF(WIN32)
    IF(MSVC) # The compiler used is MSVC
        MESSAGE(STATUS "Found MSVC compiler: ${MSVC} ${MSVC_VERSION}")
    ELSE(MSVC)
        SET(LIBRARY_OUTPUT_PATH win${BIT}/bin/${CMAKE_BUILD_TYPE})
    ENDIF(MSVC)
ELSE(WIN32)
   		SET(LIBRARY_OUTPUT_PATH ${PLATFORM}/${CMAKE_SYSTEM_PROCESSOR}-${CMAKE_BUILD_TYPE}-${BIT})
ENDIF(WIN32)
MESSAGE(STATUS "output_path: ${LIBRARY_OUTPUT_PATH}")

# TODO: under win32/64, dlls and libs should not be in the same path (non MSVC)

IF(MINGW)
	SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -mthreads")
	SET(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -mthreads")
	SET(CMAKE_EXE_LINKER_FLAGS "${CMAKE_EXE_LINKER_FLAGS} -mthreads -Wl,--export-all-symbols")
	SET(CMAKE_SHARED_LINKER_FLAGS "${CMAKE_SHARED_LINKER_FLAGS} -mthreads -Wl,--export-all-symbols")
	# For the file Message_MsgFile.cxx of TKernel we must additionally set the __TCollection_DLL define to resolve the needed HashCode() method
	SET_SOURCE_FILES_PROPERTIES("src/Message/Message_MsgFile.cxx" PROPERTIES COMPILE_FLAGS "-D__Message_DLL -D__TCollection_DLL")
ENDIF(MINGW)

# DISABLE SECURE CRT WARNINGS AND OTHER MS CRT SPECIFC THINGS
IF (MSVC)
   ADD_DEFINITIONS("/D_CRT_SECURE_NO_WARNINGS")
   ADD_DEFINITIONS("/D_CRT_NONSTDC_NO_WARNINGS")
ENDIF(MSVC)
   
IF(MSVC_IDE)
  IF(NOT ${PROJECT_NAME}_USE_MSVC_EXPRESS)
     #Use solution folders.
     SET_PROPERTY(GLOBAL PROPERTY USE_FOLDERS ON)
     SET(USE_SOLUTION_FOLDERS TRUE)
  ELSE( NOT ${PROJECT_NAME}_USE_MSVC_EXPRESS)
     SET_PROPERTY(GLOBAL PROPERTY USE_FOLDERS OFF)
  ENDIF( NOT ${PROJECT_NAME}_USE_MSVC_EXPRESS)

ENDIF(MSVC_IDE)

# Libraries are installed by default in /usr/local/lib on UNIX
# based platforms and c:/${PROJECT_NAME}-VERSION/Win[32|64]/lib on windows based
# platforms

IF(WIN32)
	SET(_PSUFFIX "Win${BIT}/")
ELSE(WIN32)
	SET(_PSUFFIX "")
ENDIF(WIN32)

# Binaries
IF(NOT DEFINED ${PROJECT_NAME}_INSTALL_BIN_DIR)
	SET(${PROJECT_NAME}_INSTALL_BIN_DIR ${_PSUFFIX}bin)
ENDIF(NOT DEFINED ${PROJECT_NAME}_INSTALL_BIN_DIR)

IF(CMAKE_CONFIGURATION_TYPES OR NMAKE)
	IF(NOT DEFINED ${PROJECT_NAME}_INSTALL_BIN_DEBUG_DIR)
		SET(${PROJECT_NAME}_INSTALL_BIN_DEBUG_DIR ${_PSUFFIX}bind)
	ENDIF(NOT DEFINED ${PROJECT_NAME}_INSTALL_BIN_DEBUG_DIR)
ENDIF(CMAKE_CONFIGURATION_TYPES OR NMAKE)

# Libraries
IF(NOT DEFINED ${PROJECT_NAME}_INSTALL_LIB_DIR)
	SET(${PROJECT_NAME}_INSTALL_LIB_DIR ${_PSUFFIX}lib)
ENDIF(NOT DEFINED ${PROJECT_NAME}_INSTALL_LIB_DIR)

IF(CMAKE_CONFIGURATION_TYPES OR NMAKE)
	IF(NOT DEFINED ${PROJECT_NAME}_INSTALL_LIB_DEBUG_DIR)
		SET(${PROJECT_NAME}_INSTALL_LIB_DEBUG_DIR ${_PSUFFIX}libd)
	ENDIF(NOT DEFINED ${PROJECT_NAME}_INSTALL_LIB_DEBUG_DIR)
ENDIF(CMAKE_CONFIGURATION_TYPES OR NMAKE)

IF(MSVC)
	IF(NOT DEFINED ${PROJECT_NAME}_INSTALL_PACKAGE_LIB_DIR)
		SET(${PROJECT_NAME}_INSTALL_PACKAGE_LIB_DIR ${${PROJECT_NAME}_INSTALL_LIB_DIR})
		IF(CMAKE_CONFIGURATION_TYPES OR NMAKE)
			SET(${PROJECT_NAME}_INSTALL_PACKAGE_LIB_DEBUG_DIR ${${PROJECT_NAME}_INSTALL_LIB_DEBUG_DIR})
		ENDIF(CMAKE_CONFIGURATION_TYPES OR NMAKE)
	ENDIF(NOT DEFINED ${PROJECT_NAME}_INSTALL_PACKAGE_LIB_DIR)
ELSE(MSVC)
	IF(NOT DEFINED ${PROJECT_NAME}_INSTALL_PACKAGE_LIB_DIR)
		SET(${PROJECT_NAME}_INSTALL_PACKAGE_LIB_DIR lib/${PROJECT_NAME})
	ENDIF(NOT DEFINED ${PROJECT_NAME}_INSTALL_PACKAGE_LIB_DIR)
ENDIF(MSVC)

# Includes
IF(NOT DEFINED ${PROJECT_NAME}_INSTALL_INCLUDE_DIR)
	SET(${PROJECT_NAME}_INSTALL_INCLUDE_DIR inc)
ENDIF(NOT DEFINED ${PROJECT_NAME}_INSTALL_INCLUDE_DIR)

# Scripts 
IF(NOT DEFINED ${PROJECT_NAME}_INSTALL_SCRIPT_DIR)
	SET(${PROJECT_NAME}_INSTALL_SCRIPT_DIR ${CMAKE_INSTALL_PREFIX})
ENDIF(NOT DEFINED ${PROJECT_NAME}_INSTALL_SCRIPT_DIR)

# Data
IF(NOT DEFINED ${PROJECT_NAME}_INSTALL_DATA_DIR)
	SET(${PROJECT_NAME}_INSTALL_DATA_DIR ${CMAKE_INSTALL_PREFIX})
ENDIF(NOT DEFINED ${PROJECT_NAME}_INSTALL_DATA_DIR)

# CMake
IF(NOT DEFINED ${PROJECT_NAME}_INSTALL_CMAKE_DATA_DIR)
	SET(${PROJECT_NAME}_INSTALL_CMAKE_DATA_DIR share/cmake)
ENDIF(NOT DEFINED ${PROJECT_NAME}_INSTALL_CMAKE_DATA_DIR)
FILE(RELATIVE_PATH ${PROJECT_NAME}_CMAKE_DATA_TO_INCLUDE_RELATIVE_DIR "${CMAKE_INSTALL_PREFIX}/${${PROJECT_NAME}_INSTALL_CMAKE_DATA_DIR}" "${CMAKE_INSTALL_PREFIX}/${${PROJECT_NAME}_INSTALL_INCLUDE_DIR}")

FOREACH(RESOURCE_FILE ${INSTALL_RESOURCE_FILES})
        GET_FILENAME_COMPONENT(RESOURCE_PATH ${RESOURCE_FILE} PATH)
        INSTALL(FILES ${RESOURCE_FILE}
                DESTINATION ${${PROJECT_NAME}_INSTALL_DATA_DIR}/${RESOURCE_PATH})
ENDFOREACH(RESOURCE_FILE ${INSTALL_RESOURCE_FILES})

CONFIGURE_FILE( ${${PROJECT_NAME}_SOURCE_DIR}/env.sh.cmake
	${${PROJECT_NAME}_BINARY_DIR}/env.sh @ONLY )
CONFIGURE_FILE( ${${PROJECT_NAME}_SOURCE_DIR}/env.csh.cmake
	${${PROJECT_NAME}_BINARY_DIR}/env.csh @ONLY )

IF(WIN32)
	SET(ENV_SCRIPTS
		${${PROJECT_NAME}_SOURCE_DIR}/env.bat
		${${PROJECT_NAME}_SOURCE_DIR}/env_build.bat
		${${PROJECT_NAME}_SOURCE_DIR}/msvc.bat
	)
ELSE(WIN32)
	SET(ENV_SCRIPTS
		${${PROJECT_NAME}_BINARY_DIR}/env.csh
		${${PROJECT_NAME}_BINARY_DIR}/env.sh
	)
ENDIF(WIN32)
INSTALL(FILES ${ENV_SCRIPTS}
	DESTINATION ${${PROJECT_NAME}_INSTALL_SCRIPT_DIR})

FILE(GLOB header_files ${${PROJECT_NAME}_SOURCE_DIR}/inc/*.*)
INSTALL(FILES ${header_files}
	DESTINATION ${${PROJECT_NAME}_INSTALL_INCLUDE_DIR} COMPONENT Development
)

SET(OCE_INSTALL_DATA_DIR ${${PROJECT_NAME}_INSTALL_DATA_DIR})
SET(OCE_DEFAULT_CSF_GraphicShr ${${PROJECT_NAME}_INSTALL_PREFIX}/${${PROJECT_NAME}_INSTALL_LIB_DIR}/${CMAKE_SHARED_LIBRARY_PREFIX}TKOpenGl${CMAKE_SHARED_LIBRARY_SUFFIX})

#this is the header used to build OCE
CONFIGURE_FILE( ${CMAKE_CURRENT_SOURCE_DIR}/oce_build_config.h.cmake
	${CMAKE_CURRENT_BINARY_DIR}/build_inc/oce-config.h )
INSTALL(FILES ${CMAKE_CURRENT_BINARY_DIR}/build_inc/oce-config.h
	RENAME oce_build_config.defs
	DESTINATION ${${PROJECT_NAME}_INSTALL_INCLUDE_DIR} COMPONENT Development )

#this is the header that is installed on the system
CONFIGURE_FILE( ${CMAKE_CURRENT_SOURCE_DIR}/oce_install_config.h.cmake
	${CMAKE_CURRENT_BINARY_DIR}/install_inc/oce-config.h )
INSTALL(FILES ${CMAKE_CURRENT_BINARY_DIR}/install_inc/oce-config.h
		DESTINATION ${${PROJECT_NAME}_INSTALL_INCLUDE_DIR} COMPONENT Development )

IF(${PROJECT_NAME}_DRAW)
	CONFIGURE_FILE( ${CMAKE_CURRENT_SOURCE_DIR}/DrawDefault.cmake
		${CMAKE_CURRENT_BINARY_DIR}/DrawDefault @ONLY )
        INSTALL(FILES ${CMAKE_CURRENT_BINARY_DIR}/DrawDefault
                DESTINATION ${${PROJECT_NAME}_INSTALL_DATA_DIR}/src/DrawResources)
ENDIF(${PROJECT_NAME}_DRAW)

# Version information
CONFIGURE_FILE (${CMAKE_CURRENT_SOURCE_DIR}/Version.rc.cmake 
				${CMAKE_CURRENT_BINARY_DIR}/Version.rc)

MACRO(PROCESS_OCC_MODULE modulename modules)
	FOREACH(module ${modules})
		LIST(APPEND ${PROJECT_NAME}_LIBRARIES ${module}) 
		SET(TOOLKIT_MODULES "")
		SET(TOOLKIT_DEPENDS "")
		SET(TOOLKIT_INCLUDE_DIRECTORIES "")
		SET(TOOLKIT_LIBS "")
		ADD_SUBDIRECTORY( adm/cmake/${module} )

		IF (USE_SOLUTION_FOLDERS)
      SET_TARGET_PROPERTIES(${module} PROPERTIES FOLDER ${modulename} )
    ENDIF(USE_SOLUTION_FOLDERS)
    
	ENDFOREACH(module ${modules})
ENDMACRO(PROCESS_OCC_MODULE modulename modules)

####################################################################################
# Process modules of each ToolKit                                                  #
####################################################################################
# Foundation
PROCESS_OCC_MODULE( "Kernel" "TKernel;TKMath;TKAdvTools" )

# Modeling Data
IF (${PROJECT_NAME}_MODEL)
	PROCESS_OCC_MODULE( "Modeling Data" "TKG2d;TKG3d;TKGeomBase;TKBRep" )
ENDIF (${PROJECT_NAME}_MODEL)

# Modeling Algorithms
IF (${PROJECT_NAME}_MODEL)
	PROCESS_OCC_MODULE( "Modeling Algorithms" "TKGeomAlgo;TKTopAlgo;TKPrim;TKBO;TKHLR;TKMesh;TKShHealing;TKXMesh;TKBool;TKFillet;TKFeat;TKOffset" )
ENDIF (${PROJECT_NAME}_MODEL)

# Visualisation
IF(${PROJECT_NAME}_VISUALISATION)
	PROCESS_OCC_MODULE( "Visualization" "TKService;TKV2d;TKV3d" )
ENDIF(${PROJECT_NAME}_VISUALISATION)
IF(${PROJECT_NAME}_VISUALISATION_FULL)
	PROCESS_OCC_MODULE( "Visualization" "TKOpenGl;TKMeshVS;TKNIS;TKVoxel" )
ENDIF(${PROJECT_NAME}_VISUALISATION_FULL)

# Application framework
IF(${PROJECT_NAME}_OCAF)
	PROCESS_OCC_MODULE( "OCAF" "TKCDF;PTKernel;TKLCAF;FWOSPlugin;TKPShape;TKCAF;TKBinL;TKXmlL;TKPLCAF;TKTObj;TKShapeSchema;TKBin;TKXml;TKPCAF;TKBinTObj;TKXmlTObj;TKStdLSchema;TKStdSchema" )
ENDIF(${PROJECT_NAME}_OCAF)

# Data exchange
IF(${PROJECT_NAME}_DATAEXCHANGE)
	PROCESS_OCC_MODULE( "Data Exchange" "TKVRML;TKXCAF;TKSTL;TKXSBase;TKXCAFSchema;TKXmlXCAF;TKBinXCAF;TKSTEPBase;TKIGES;TKSTEPAttr;TKSTEP209;TKXDEIGES;TKSTEP;TKXDESTEP" )
ENDIF(${PROJECT_NAME}_DATAEXCHANGE)

# Draw
IF(${PROJECT_NAME}_DRAW)
	PROCESS_OCC_MODULE( "Draw" "TKDraw;TKTopTest;TKViewerTest;TKXSDRAW;TKDCAF;TKXDEDRAW;TKTObjDRAW" )
ENDIF(${PROJECT_NAME}_DRAW)

# DRAWEXE application
IF(${PROJECT_NAME}_DRAW)
	PROCESS_OCC_MODULE( "Draw" DRAWEXE )
ENDIF(${PROJECT_NAME}_DRAW)

CONFIGURE_FILE( ${CMAKE_CURRENT_SOURCE_DIR}/ProjectConfig.cmake.in 
	${CMAKE_CURRENT_BINARY_DIR}/${PROJECT_NAME}Config.cmake @ONLY )
INSTALL(FILES ${PROJECT_BINARY_DIR}/${PROJECT_NAME}Config.cmake DESTINATION ${${PROJECT_NAME}_INSTALL_CMAKE_DATA_DIR} COMPONENT Development )

IF(NOT (WIN32 AND (CMAKE_CONFIGURATION_TYPES OR NMAKE)))
INSTALL(EXPORT ${PROJECT_NAME}-libraries DESTINATION ${${PROJECT_NAME}_INSTALL_CMAKE_DATA_DIR} COMPONENT Development)
ENDIF(NOT (WIN32 AND (CMAKE_CONFIGURATION_TYPES OR NMAKE)))

###############################################################################
# OCE Packaging                                                               #
# $make package                                                               #
###############################################################################

SET(CPACK_PACKAGE_DESCRIPTION_SUMMARY "OpenCASCADE Community Edition")
SET(CPACK_PACKAGE_DESCRIPTION_FILE "${CMAKE_CURRENT_SOURCE_DIR}/README.txt")
SET(CPACK_RESOURCE_FILE_LICENSE "${CMAKE_CURRENT_SOURCE_DIR}/LICENSE.txt")
SET(CPACK_SET_DESTDIR "ON")

SET(CPACK_PACKAGE_VERSION_MAJOR ${OCE_VERSION_MAJOR})
SET(CPACK_PACKAGE_VERSION_MINOR ${OCE_VERSION_MINOR})
SET(CPACK_PACKAGE_VERSION_PATCH ${OCE_VERSION_PATCH})
SET(CPACK_PACKAGE_NAME ${PROJECT_NAME} )

INCLUDE(CPack)
